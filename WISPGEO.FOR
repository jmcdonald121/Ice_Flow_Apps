CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC00000010
C                                                                      C00000020
C      *INTERACTIVE GEODOP*       (11/27/87) (MAINFRAME)               C00000030
C                                 (11/24/89) (IBM PC-AT)               C
C                                                                      C00000040
C       THIS PROGRAM CREATES THE INPUT FILE TO RUN THE GEODOP          C00000050
C       SOFTWARE PROGRAM OF THE GEODOP V SYSTEM.  THE GEODOP V         C00000060
C       SYSTEM OBTAINS ACCURATE POSITIONS ON THE SURFACE OF THE        C00000070
C       EARTH BY REDUCING THE DATA FROM THE TRANSIT (DOPPLER)          C00000080
C       SATELLITE SYSTEM.  EXECUTION OF THIS FORTRAN PROGRAM           C00000090
C       WILL PROMPT THE USER TO INPUT DATA FROM THE KEYBOARD.  THE     C00000100
C       DATA IS THEN WRITTEN TO THE FILE *GE.INP.                      C00000110
C          THE GEODOP SOFTWARE PROGRAM COMPUTES THE GEOCENTRIC         C00000140
C       COORDINATES OF THE SITES FROM THE DOPPLER DATA.  POSITIONS     C00000150
C       CAN BE COMPUTED USING DATA FROM ONE SITE, WHICH IS CALLED      C00000160
C       POINT POSITIONING, OR BY USING DATA AT MULTIPLE SITES          C00000170
C       RECORDING AT THE SAME TIME, WHICH IS CALLED THE SHORT ARC      C00000180
C       TECHNIQUE (WE HERE AT B.P.R.C. CALL IT NETWORK ADJUSTMENT).    C00000190
C       THE BINARY INPUT DATA IS A PREDOP-LIKE DATASET, GENERATED BY   C
C       EITHER PREDOP, PREPAR, OR MERGE.                               C00000210
C          THIS IS THE IBM PC-AT VERSION OF THE MAINFRAME PROGRAM.     C
C       THIS PROGRAM HAS BEEN ALTERED SO IT CAN ONLY PROCESS THE       C
C       TRANSIT DATA IN THE POINT POSITIONING MODE.                    C
C          ALL OF THE VARIABLE NAMES USED FOR WRITING TO THE GEODOP    C
C       INPUT FILE CORRESPOND TO THE INTERNAL VARIABLE NAMES IN        C
C       THE GEODOP FORTRAN SOURCE CODE (SEE REFERENCE BELOW)           C
C                                                                      C00000220
C       REFERENCES:                                                    C00000230
C         1. ARCHINAL, B. A., 1986.  DOCUMENTATION FOR THE IBM         C00000240
C              VERSION OF THE GEODOP V PROGRAM SYSTEM, VERSION         C00000250
C              2.0. UNPUBLISHED.  DEPARTMENT OF GEODETIC SCIENCE       C00000260
C              AND SURVEYING.                                          C00000270
C                                                                      C00000280
C         2. KOUBA, J. AND J.D. BOAL.  1976.  PROGRAM GEODOP.          C
C              DEPARTMENT OF ENERGY, MINES AND RESOURCES, OTTAWA,      C
C              ONTARIO. (N.B.: NOW A PART OF THE CANADIAN GEOLOGIC     C
C              SURVEY)                                                 C
C                                                                      C
C       JAMES MCDONALD                                                 C00000290
C       BYRD POLAR RESEARCH CENTER                                     C00000300
C       OHIO STATE UNIVERSITY                                          C00000310
C       COLUMBUS, OHIO  43210                                          C00000320
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC00000330
      CHARACTER*1 STNM(30),FILE1(30),FILE2(6),FILE3(6),BLANK
      CHARACTER*4 LOC,PE,TMM,SIGMA,YES                                  00000350
      CHARACTER*30 INPUT
      REAL*8 X,Y,Z
      COMMON /A/ A1,B1,FOFFS(5),FS(7),ITTG,MRCN,NDLY,OUT,PHWTM(3),
     1           PSS,RRS,RT,STNM,STWGHT(6),X,Y,YRS,Z
      COMMON /B/ FILE1,INPUT,NDES,NUMPE,DELTST(6)
      INTEGER SMXPS,SIGDES                                                     00000420
      DATA TMM/'PE'/,YES/'Y'/,FILE2/'G','E','.','T','E','X'/,           00000440
     1     FILE3/'G','E','.','I','N','P'/,BLANK/' '/
      LOC=TMM                                                           00000450
C-------READ THE NUMBER OF PRECISE EPHEMERIS FILES---------------------C
      OPEN(3,FILE='GEODOP3.SCR',STATUS='OLD')
      READ(3,901) NUMPE
901   FORMAT(I1)
      CLOSE(3)
C-------SELECTION FOR WHETHER GEODOP IS BEING RUN FOR INITIAL----------C00000660
C-------POSITIONS, FINAL POSITIONS, OR RECEIVER TIME DELAY-------------C00000670
C-------I.E. THE NUMBER OF TIMES THAT GEODOP HAS BEEN RUN--------------C
      OPEN(7,FILE='GEODOP1.SCR',STATUS='OLD')
      READ(7,606) NDES                                                  00000720
606   FORMAT(I1)                                                        00000730
C-------INPUT THE SITE NAME--------------------------------------------C
      READ(7,900) (STNM(I),I=1,30)
900   FORMAT(30A1)
      DO 2 I=1,30
         IF(STNM(I).EQ.BLANK) GOTO 4
2     CONTINUE
4     I=I-1
      DO 3 J=1,I
         FILE1(J)=STNM(J)
3        INPUT(J:30)=STNM(J)
      DO 5 J=1,6
         FILE1(I+J)=FILE2(J)
5        INPUT(I+J:30)=FILE3(J)
C-------DETERMINE WHETHER THE PRECISE EPHEMERIS IS INCLUDED------------C00000760
C-------IN THE GEODOP RUN----------------------------------------------C00000770
      READ(7,699) PE                                                    00000870
699   FORMAT(A4)
C*******FIRST RUN (NDES=1)*********************************************C
      IF(NDES.EQ.1) THEN
C-------INPUT ESTIMATED COORDINATES------------------------------------C
         READ(7,607) X,Y,Z
607      FORMAT(2(D11.2,1X),D11.2)
C-------INPUT PREVIOUS ELLIPSOID PARAMETERS----------------------------C
         READ(7,608) A1,B1
608      FORMAT(2(F7.3,1X))
C-------INPUT MEDIAN DAY OF THE YEAR OF THE DOPPLER DATA FOR THE-------C00001360
C-------SATELLITE FREQUENCY OFFSETS------------------------------------C00001370
         WRITE(0,107)                                                   00001380
107      FORMAT(' ','REFERENCE EPOCH FOR THE SATELLITE OFFSETS (DAY OF Y
     1EAR, 0.-366.)',/)                                                 00001400
         READ(0,*) FS(7)                                                00001440
         WRITE(0,110) FS(7)                                             00001460
110      FORMAT(' ',/,6X,F4.0,/)                                        00001470
C-------INPUT THE YEAR OF THE DOPPLER DATA-----------------------------C00001550
         WRITE(0,111)                                                   00001560
111      FORMAT(' ','YEAR OF THE DOPPLER DATA BEING PROCESSED.  NOTE THA
     1T THE PROGRAM CANNOT HANDLE',/,1X,'DATA CROSSING INTO TWO YEARS.',
     2/)                                                                00001580
         READ(0,*) OUT                                                  00001620
         WRITE(0,114) OUT                                               00001640
114      FORMAT(' ',/,6X,F5.0,/)                                        00001650
         YRS=OUT
C--------OUTPUT OPTION-------------------------------------------------C00002330
         WRITE(0,200)                                                   00002340
200      FORMAT(' ','OUTPUT OPTIONS:',/,3X,'1: EXTENDED OUTPUT',/,3X,'2:
     1 SHORT (NORMAL) OUTPUT',/)                                        00002360
         READ(0,201) ITTG                                               00002370
201      FORMAT(I1)                                                     00002380
         WRITE(0,202) ITTG                                              00002390
202      FORMAT(' ',/,6X,I1,/)                                          00002400
         IF(ITTG.EQ.2) ITTG=5                                           00002410
C-------ELLIPSOID PARAMETERS-------------------------------------------C
         WRITE(0,236)
236      FORMAT(' ','REFERENCE ELLIPSOID',/,3X,'1   WGS-66  [DEFAULT]',/
     1,3X,'2   WGS-72',/,3X,'3   WGS-84',/,3X,'4   USE PREVIOUS ELLIPSOI
     2D PARAMETERS',/,3X,'5   INPUT VALUES FROM THE KEYBOARD',/)
         READ(0,*) IELLIP
         GOTO(60,61,62,63,64),IELLIP
60          A1=145.0
            B1=759.77
            WRITE(0,601)
601         FORMAT(' ',/,6X,'1   WGS-66',/)
            GOTO 65
61          A1=135.0
            B1=750.50
            WRITE(0,611)
611         FORMAT(' ',/,6X,'2   WGS-72',/)
            GOTO 65
62          A1=137.00
            B1=752.314
            WRITE(0,621)
621         FORMAT(' ',/,6X,'3   WGS-84',/)
            GOTO 65
63          CONTINUE
            WRITE(0,2401) A1
2401        FORMAT(' ',/,6X,'A=',F7.3,/)
            WRITE(0,2411) B1
2411        FORMAT(' ',/,6X,'B=',F7.3,/)
            GOTO 65
64          WRITE(0,237)                                                00003690
237         FORMAT(' ','TYPE IN THE REFERENCE ELLIPSOID SEMI-MAJOR AXIS 
     1- 6378000 (METERS).  DEFAULT IS NWL-9D (WGS 66) ELLIPSOID.',/)    00003710
            READ(0,238) A1                                              00003720
 238        FORMAT(F7.3)                                                00003730
            WRITE(0,2401) A1
            WRITE(0,239)                                                00003740
 239        FORMAT(' ','TYPE IN THE REFERENCE ELLIPSOID SEMI-MINOR AXIS 
     1- 6356000 (METERS).  DEFAULT IS NWL-9D (WGS-66) ELLIPSOID.',/)    00003760
            READ(0,238) B1                                              00003770
            WRITE(0,2411) B1
65       CONTINUE
C--------CORRELATION MODEL---------------------------------------------C00002420
         RT=0.0                                                         00002460
C--------ORBITAL ERRORS------------------------------------------------C
         IF(PE.NE.LOC) THEN                                             00002550
            DO 97 I=1,6                                                 00002560
97             STWGHT(I)=20.0                                           00002570
         ELSE                                                           00002580
            DO 98 I=1,6                                                 00002590
98             STWGHT(I)=2.0                                            00002600
         ENDIF                                                          00002610
C--------INPUT BETTER ESTIMATES OF THE KEPLERIAN ELEMENTS--------------C00002770
         WRITE(0,203)                                                   00002630
203      FORMAT(' ','DO YOU NEED BETTER ESTIMATES OF THE SIGMA''S OF THE
     1 ORBITAL',/,1X,'PARAMETERS THAN THE DEFAULT VALUES (Y/N)',/)      00002650
         READ(0,204) SIGMA                                              00002660
204      FORMAT(A1)                                                     00002670
         WRITE(0,2041) SIGMA
2041     FORMAT(' ',/,6X,A1,/)
         IF(SIGMA.EQ.YES) THEN                                          00002690
            WRITE(0,205)                                                00002790
205         FORMAT(' ','THE NEXT 6 INPUTED VALUES ARE THE SIGMAS OF THE 
     16 KEPLERIAN ELEMENTS')                                            00002810
            WRITE(0,206)                                                00002820
206         FORMAT(' ','1. SEMI-MAJOR AXIS (A) ORBITAL CONSTRAINT, ONE S
     1IGMA (METERS).',/)                                                00002840
            READ(0,*) STWGHT(1)                                         00002870
            WRITE(0,209) STWGHT(1)                                      00002890
209         FORMAT(' ',/,6X,F5.2,/)                                     00002900
            WRITE(0,210)                                                00002910
210         FORMAT(' ','2. MEAN MOTION (N) ORBITAL CONSTRAINT, ONE SIGMA
     1 (METERS)',/)                                                     00002930
            READ(0,*) STWGHT(2)                                         00002950
            WRITE(0,209) STWGHT(2)                                      00002960
            WRITE(0,211)                                                00002970
211         FORMAT(' ','3. INCLINATION ANGLE (I) ORBITAL CONTRAINT, ONE 
     1SIGMA (METERS)',/)                                                00002990
            READ(0,*) STWGHT(3)                                         00003010
            WRITE(0,209) STWGHT(3)                                      00003020
            WRITE(0,212)                                                00003030
212         FORMAT(' ','4. RIGHT ASCENSION OF ASCENDING NODE (CAPITAL OM
     1EGA) ORBITAL CONSTRAINT,',/,1X,'ONE SIGMA (METERS).',/)           00003050
            READ(0,*) STWGHT(4)                                         00003070
            WRITE(0,209) STWGHT(4)                                      00003080
            WRITE(0,213)                                                00003090
213         FORMAT(' ','5. ECCENTRICITY (E) ORBITAL CONTRAINT, ONE SIGMA
     1 (METERS).',/)                                                    00003110
            READ(0,*) STWGHT(5)                                         00003130
            WRITE(0,209) STWGHT(5)                                      00003140
            WRITE(0,214)                                                00003150
214         FORMAT(' ','6. ARGUMENT OF PERIGEE (SMALL OMEGA) ORBITAL CON
     1STRAINT, ONE SIGMA (METERS).',/)                                  00003170
            READ(0,*) STWGHT(6)                                         00003190
            WRITE(0,209) STWGHT(6)                                      00003200
         ENDIF                                                          00003210
C--------SWITCH TO TURN ON ANALYSIS OF CORELATION FOR FIRST RUNS-------C00001320
         MRCN=1                                                         00001340
C-------INPUT PARAMETERS THAT WILL CHANGE------------------------------C
         RRS=15.0
         NDLY=100
         FOFFS(5)=50.0
         FOFFS(1)=0.0
         FOFFS(2)=0.2
         FOFFS(3)=0.0
         WRITE(0,350)
350      FORMAT(' ','POSITION UNCERTAINTY',/,3X,'1   25 METERS',/,3X,'2 
     1  INPUT VALUE FROM THE KEYBOARD',/)
         READ(0,*) SIGDES
         GOTO(380,381),SIGDES
380         DO 34 K=1,3                                                 00004670
34             PHWTM(K)=625.0                                           00004680
            WRITE(0,3801)
3801        FORMAT(' ',/,6X,'1   25 METERS',/)
            GOTO 382
381         WRITE(0,351)
351         FORMAT(' ','INPUT THE POSITIONAL UNCERTAINTY (UP TO 10 KM)',
     1/)
            READ(0,*) SIGPOS
            WRITE(0,3811) SIGPOS
3811        FORMAT(' ',/,6X,F10.2,/)
            DO 35 K=1,3
35             PHWTM(K)=SIGPOS**2
382      CONTINUE
C*******INPUT THE IMPROVED PARAMETERS (NDES=2,3,4)*********************C00004260
      ELSE                                                              00004280
         OPEN(2,FILE=INPUT,STATUS='OLD')
         READ(2,300) X,Y,Z
300      FORMAT(39(/),2(D11.2,1X),D11.2)
         READ(2,301) RRS
301      FORMAT(/,3X,F6.2)
         READ(2,302) NDLY,FOFFS(5)
302      FORMAT(I3,1X,F5.0)
         READ(2,303) (FOFFS(I),I=1,3)
303      FORMAT(2(F6.2,1X),/,F6.2)
         CLOSE(2)
         OPEN(8,FILE='GEODOP2.SCR',STATUS='OLD')
C--------SWITCH FOR TURNING OFF ANALYSIS OF CORELATION SWITCH----------C00001170
C--------FOR SUBSEQUENT RUNS AND TIME DELAY ANALYSIS-------------------C00001180
         MRCN=0                                                         00001190
C--------INPUT THE SIGMA'S OF THE ORBITAL VALUES-----------------------C00002480
C-------DEFAULT VALUES OF THE KEPLERIAN ELEMENTS FOR NDES=3------------C00002490
         IF(NDES.EQ.3) THEN                                             00002500
            DO 20 I=1,6                                                 00002510
20             DELTST(I)=0.01                                           00002520
         ENDIF
C-------READ VALUES OF THE KEPLERIAN ELEMENTS FOR NDES=2 AND 4---------C00002530
         READ(7,21) (STWGHT(I),I=1,6)
21       FORMAT(6(F5.2,1X))
C-------CORRELATION MODEL----------------------------------------------C
         IF(NDES.NE.3) THEN                                             00002430
            RT=2.0                                                      00002440
         ELSE                                                           00002450
            RT=0.0                                                      00002460
         ENDIF                                                          00002470
C-------DECIDE ON THE DEFAULT COORDINATE VARIENCES---------------------C00004290
C-------COORDINATE VARIENCES FOR SUBSEQUENT RUNS-----------------------C00004480
         IF(NDES.EQ.2.OR.NDES.EQ.4) THEN                                00004490
            DO 44 K=1,3                                                 00004670
44             PHWTM(K)=625.0                                           00004680
C-------COORDINATE VARIENCE FOR RECEIVER TIME DELAY ANALYSIS-----------C00004700
         ELSE                                                           00004710
            DO 45 K=1,3                                                 00004720
45             PHWTM(K)=1.0E-09                                         00004730
         ENDIF                                                          00004740
C-------RANGE RATE SIGMA-----------------------------------------------C00004760
         IF(NDES.EQ.2) THEN
            READ(8,46) RRS                                              00004800
46          FORMAT(F6.2)
         ENDIF
C-------INPUT INPROVED COORDINATES-------------------------------------C
         IF(NDES.EQ.2.OR.NDES.EQ.3) THEN
            READ(8,800) X,Y,Z
800         FORMAT(2(D11.2,1X),D11.2)
         ENDIF
C-------RECEIVER TIME DELAY--------------------------------------------C00004830
         IF(NDES.EQ.4) THEN                                             00004840
            READ(8,452) ITEMP                                           00004910
452         FORMAT(I5)																	     
         IF(ITEMP.GE.49.AND.ITEMP.LE.151) NDLY=ITEMP
C-------RECEIVER TIME DELAY SIGMA--------------------------------------C00004950
            FOFFS(5)=50.0                                               00004960
C-------RECEIVER TIME DELAY AND SIGMA FOR------------------------------C00004970
C-------RECEIVER TIME DELAY DETERMINATION------------------------------C00004980
         ELSEIF(NDES.EQ.3) THEN                                         00004990
            NDLY=100                                                    00005000
            FOFFS(5)=500.0                                              00005010
         ELSE
            NDLY=100
            FOFFS(5)=50.0
         ENDIF                                                          00005020
C-------RECEIVER FREQUENCY OFFSET--------------------------------------C00005030
         IF(NDES.EQ.2) THEN
            READ(8,453) FOFFS(1)                                        00005070
453         FORMAT(F6.2)
         ENDIF
C-------RECEIVER FREQUENCY DRIFT---------------------------------------C00005100
         IF(NDES.EQ.2) THEN
            WRITE(0,420)                                                00005110
420         FORMAT(' ','TYPE IN THE FREQUENCY DRIFT (IN HERTZ/HOUR)',/) 
            READ(0,*) FOFFS(3)                                          00005140
            WRITE(0,421) FOFFS(3)                                       00005150
421         FORMAT(' ',/,6X,E11.4,/)                                    00005160
            FOFFS(3)=FOFFS(3)*24.0/0.04
         ENDIF
         CLOSE(8,STATUS='DELETE')
      ENDIF                                                             00005190
      CALL WRGEO                                                        00005450
      REWIND 7
      NDES=NDES+1
      WRITE(7,422) NDES,(STNM(I),I=1,30),PE,(STWGHT(I),I=1,6)
422   FORMAT(I1,/,30A1,/,A4,/,6(F5.2,1X))
      CLOSE(7)
      STOP                                                              00005460
      END                                                               00005470
      SUBROUTINE WRGEO                                                  00005480
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC00005490
C                                                                      C00005500
C       *SUBROUTINE WRGEO*       (11/27/87) (MAINFRAME)                C00005510
C                                (11/24/89) (IBM PC-AT)                C
C                                                                      C00005520
C       THIS SUBROUTINE WRITES THE INPUTED DATA TO THE FILE            C00005530
C       *GE.INP.                                                       C00005540
C                                                                      C00005550
C       JAMES MCDONALD                                                 C00005560
C       BYRD POLAR RESEARCH CENTER                                     C00005570
C       O.S.U.                                                         C00005580
C       COLUMBUS, OHIO  43210                                          C00005590
C                                                                      C00005600
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC00005610
      CHARACTER*1 STNM(30),FILE1(30)                                    00005630
      CHARACTER*30 INPUT
      REAL*8 X,Y,Z
      INTEGER ASG,SMXPS
      DIMENSION DXS(3),SHIFT(3)  
      COMMON /A/ A1,B1,FOFFS(5),FS(7),ITTG,MRCN,NDLY,OUT,PHWTM(3),
     1           PSS,RRS,RT,STNM,STWGHT(6),X,Y,YRS,Z
      COMMON /B/ FILE1,INPUT,NDES,NUMPE,DELTST(6)
      MXPS=1                                                            00001040
      SMXPS=MXPS                                                        00001060
      NOBI=4
      MXDOP=32
      NORB=6
      NSGM=5                                                            00001270
      NSAT=0
      TRPCON=10.
      ASG=0.0
      HOR=7.5
      ITRM=0
      IWRT=0
      SIGA=0.0
      DO 1 I=1,3
         DXS(I)=0.0
1     CONTINUE
      ITE=0
      NPASS=0
      N99PR=0
      NZDFR=0
      NLELR=0
      ND99R=0
      NFREE=0
      VPV=0
      NTEST=0
      PSS=2.0
      FOFFS(4)=15.0
      DO 2 I=1,3
         SHIFT(I)=0.0
2     CONTINUE
      IF(NUMPE.NE.0) NUMPE=NUMPE+1
C*******WRITE THE OUTPUT***********************************************C
C-------FIRST RUN------------------------------------------------------C
      IF(NDES.EQ.1) THEN
         OPEN(2,FILE=INPUT,STATUS='NEW')
         IF(NUMPE.EQ.0) THEN
            WRITE(2,99) (FILE1(I),I=1,30),MXPS,SMXPS,NOBI,MXDOP,NORB,
     1                  NSGM,NSAT,TRPCON   
99          FORMAT('GEODOP.BIN',//,30A1,///,7(/,I2),/,F4.1)             00005990
         ELSE
            WRITE(2,100) NUMPE,(FILE1(I),I=1,30),MXPS,SMXPS,NOBI,MXDOP,
     1                   NORB,NSGM,NSAT,TRPCON   
100         FORMAT('GEODOP',I1,'.BIN',//,30A1,///,7(/,I2),/,F4.1)       00005990
         ENDIF
         WRITE(2,101) YRS,OUT,FS(7),FS(1)                               00006020
101      FORMAT(F5.0,/,F5.0,1X,F4.0,/,F2.0)                             00006030
         WRITE(2,102) ITTG,ASG,HOR,ITRM,SIGA                            00006120
102      FORMAT(I1,/,I1,/,F4.1,/,I1,/,F3.1)                             00006130
         WRITE(2,103) A1,B1,(DXS(I),I=1,3),ITE,RT,IWRT,(STWGHT(I),I=1,6)00006160
103      FORMAT(F7.3,/,F7.3,/,3(F2.0,1X),/,I1,/,F2.0,/,I1,/,6(F5.2,1X)) 00006170
         WRITE(2,104) MRCN,NPASS,N99PR,NZDFR,NLELR,ND99R,NFREE,VPV      00006240
104      FORMAT(7(I1,/),F2.0)                                           00006250
         WRITE(2,105) (STNM(I),I=1,30),NTEST,X,Y,Z,(SHIFT(I),I=1,3)     00006450
105      FORMAT(30A1,/,I1,/,2(F11.2,1X),F11.2,/,3(F2.0,1X))             00006470
         WRITE(2,106) PSS,RRS,NDLY,FOFFS(5),(FOFFS(I),I=1,4)
106      FORMAT(F2.0,1X,F6.2,/,I3,1X,F5.0,/,2(F6.2,1X),/,2(F6.2,1X))
         WRITE(2,107) (PHWTM(J),J=1,3)                                  000065
107      FORMAT('Y',/,3(F12.2,1X),/,'N')                                00006600
C------NDES=2 OR 4-----------------------------------------------------C
      ELSEIF(NDES.EQ.2.OR.NDES.EQ.4) THEN
         OPEN(2,FILE=INPUT,STATUS='OLD')
         READ(2,201)
201      FORMAT(25(/))
         WRITE(2,203) RT,IWRT,(STWGHT(I),I=1,6)                         00006160
203      FORMAT(F2.0,/,I1,/,6(F5.2,1X))                                 00006170
         WRITE(2,104) MRCN,NPASS,N99PR,NZDFR,NLELR,ND99R,NFREE,VPV      00006240
         WRITE(2,105) (STNM(I),I=1,30),NTEST,X,Y,Z,(SHIFT(I),I=1,3)     00006450
         WRITE(2,106) PSS,RRS,NDLY,FOFFS(5),(FOFFS(I),I=1,4)
         WRITE(2,107) (PHWTM(J),J=1,3)                                  000065
C------NDES=3--USE DELTST(I) INSTEAD OF STWGHT(I)----------------------C
      ELSE
         OPEN(2,FILE=INPUT,STATUS='OLD')
         READ(2,201)
         WRITE(2,203) RT,IWRT,(DELTST(I),I=1,6)                         00006160
         WRITE(2,104) MRCN,NPASS,N99PR,NZDFR,NLELR,ND99R,NFREE,VPV      00006240
         WRITE(2,105) (STNM(I),I=1,30),NTEST,X,Y,Z,(SHIFT(I),I=1,3)     00006450
         WRITE(2,106) PSS,RRS,NDLY,FOFFS(5),(FOFFS(I),I=1,4)
         WRITE(2,301)                                                   000065
301      FORMAT('Y',/,'1.0D-09 1.0D-09 1.0D-09',/,'N')
      ENDIF
      RETURN                                                            00006700
      END                                                               00006710
